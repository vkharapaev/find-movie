plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.google.gms.google-services'
    id 'androidx.navigation.safeargs.kotlin'
}

android {
    signingConfigs {
        release {
            Properties properties = new Properties()
            properties.load(project.rootProject.file("signing.properties").newDataInputStream())
            storeFile file(properties.getProperty("store_file"))
            storePassword properties.getProperty("store_pass")
            keyAlias properties.getProperty("key_alias")
            keyPassword properties.getProperty("pass")
        }
    }
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.headmostlab.findmovie"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 2
        versionName "3"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation":
                                      "$projectDir/schemas".toString()]
            }
        }
        signingConfig signingConfigs.release
    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildTypes.each {
        Properties properties = new Properties()
        properties.load(project.rootProject.file("apikey.properties").newDataInputStream())
        def apiKey = properties.getProperty("tmdb_api_key", "")
        it.buildConfigField "String", "TMDB_API_KEY", apiKey
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    // Dagger
    implementation 'com.google.dagger:dagger:2.33'
    kapt 'com.google.dagger:dagger-compiler:2.33'

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:2.3.5"
    implementation "androidx.navigation:navigation-ui-ktx:2.3.5"

    implementation 'com.github.HaarigerHarald:android-youtubeExtractor:2.1.0'

    implementation 'com.google.android.exoplayer:exoplayer:2.13.3'

    implementation 'com.github.rubensousa:gravitysnaphelper:2.2.1'

    implementation "androidx.fragment:fragment-ktx:1.3.3"

    implementation 'com.facebook.shimmer:shimmer:0.5.0@aar'

    // RxJava
    implementation 'io.reactivex.rxjava2:rxjava:2.2.20'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'

    // Retrofit + Gson + OkHttp
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.0'

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    kapt 'com.github.bumptech.glide:compiler:4.12.0'

    // Room
    implementation 'androidx.room:room-runtime:2.3.0'
    implementation 'androidx.room:room-rxjava2:2.3.0'
    kapt 'androidx.room:room-compiler:2.3.0'

    // Paging
    implementation "androidx.paging:paging-runtime-ktx:3.0.0"
    implementation "androidx.paging:paging-rxjava2-ktx:3.0.0"

    implementation 'com.google.firebase:firebase-messaging:21.1.0'

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'
    implementation "androidx.lifecycle:lifecycle-reactivestreams-ktx:2.3.1"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.3.1"

    implementation 'androidx.core:core-ktx:1.6.0-alpha03'

    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}